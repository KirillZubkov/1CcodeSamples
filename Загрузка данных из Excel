#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьКодыКРСТизExcel(Команда)
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбора.Заголовок = "Выберите файл";
	Если ДиалогВыбора.Выбрать() Тогда
		ИмяФайла = ДиалогВыбора.ПолноеИмяФайла;
	Иначе
		Возврат;
	КонецЕсли;
	
	Файл = Новый Файл(ИмяФайла);
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
	ЗагрузитьКодыКРСТизExcelНаСервере(ДвоичныеДанные, Файл.Расширение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузитьКодыКРСТизExcelНаСервере(ДвоичныеДанные, Расширение)
	
	ИмяФайлаНаСервере = ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанные.Записать(ИмяФайлаНаСервере);
	
	Попытка
		Excel = Новый COMОбъект("Excel.Application");
		Excel.WorkBooks.Open(ИмяФайлаНаСервере);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
		Возврат;
	КонецПопытки;
	
	Попытка
		Excel.Sheets(1).Select();
	Исключение
		Excel.ActiveWorkbook.Close();
		Excel = 0;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Файл " + Строка(ИмяФайлаНаСервере) + " не соответствует необходимому формату. Первый лист не найден");
		Возврат;
	КонецПопытки;
	
	ТЗкодыКРСТ = Новый ТаблицаЗначений;
	ТЗкодыКРСТ.Колонки.Добавить("КодОКТМО", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(11)));
	ТЗкодыКРСТ.Колонки.Добавить("КодКРСТ", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(9)));
	
	Для НомерСтроки = 1 По Excel.Cells(1,1).SpecialCells(11).Row Цикл
		Строка = ТЗкодыКРСТ.Добавить();
		Строка.КодОКТМО = СокрЛП(Excel.Cells(НомерСтроки, 1).Text);
		Строка.КодКРСТ = СокрЛП(Excel.Cells(НомерСтроки, 6).Text);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗкодыКРСТ.КодОКТМО КАК КодОКТМО,
		|	ТЗкодыКРСТ.КодКРСТ КАК КодКРСТ
		|ПОМЕСТИТЬ втКодыКРСТ
		|ИЗ
		|	&ТЗкодыКРСТ КАК ТЗкодыКРСТ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КодОКТМО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СельскиеТерриторииАгломерацииСрезПоследних.СельскиеТерритории КАК Ссылка,
		|	втКодыКРСТ.КодКРСТ КАК КодКРСТ
		|ИЗ
		|	РегистрСведений.СельскиеТерриторииАгломерации.СрезПоследних КАК СельскиеТерриторииАгломерацииСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКодыКРСТ КАК втКодыКРСТ
		|		ПО СельскиеТерриторииАгломерацииСрезПоследних.ОКТМО = втКодыКРСТ.КодОКТМО";
	
	Запрос.УстановитьПараметр("ТЗкодыКРСТ", ТЗкодыКРСТ);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ТерриторияОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ЗначениеЗаполнено(ТерриторияОбъект.КодКРСТ) И ТерриторияОбъект.КодКРСТ <> Выборка.КодКРСТ Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрШаблон("Для территории с кодом ОКТМО %1 уже задан код КРСТ %2, а в файле указан %3", 
				ТерриторияОбъект.КодОКТМО, ТерриторияОбъект.КодКРСТ, Выборка.КодКРСТ));
		ИначеЕсли НЕ ЗначениеЗаполнено(ТерриторияОбъект.КодКРСТ) Тогда 
			ТерриторияОбъект.КодКРСТ = Выборка.КодКРСТ;
			ТерриторияОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	
	Excel.Workbooks.Close();
	Excel.Application.Quit();
	
КонецПроцедуры

#КонецОбласти
